buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
    }
	
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
		classpath 'org.ajoberstar:grgit:1.1.0'
	}
}

apply plugin: 'forge'
apply plugin: 'java'

version = newFSPVersion

if (project.hasProperty('dev')) {
	def git = org.ajoberstar.grgit.Grgit.open(file('.'))
	project.version = project.version + '-' + git.head().abbreviatedId + '-dev'
}

group = "com.flaxbeard.Steamcraft"
archivesBaseName = "FSP-1.7.10"

sourceCompatibility = 1.7
targetCompatibility = 1.7

minecraft {
	version = forgeVersion
	runDir = "eclipse"
	replace "@VERSION@", project.version
}

repositories {
	maven {
		name = "ic2"
		url = "http://maven.ic2.player.to/"
	}

	maven {
		name "progwml6"
		url 'http://dvs1.progwml6.com/files/maven'
	}

	maven {
		name "tterrag Repo"
		url "http://maven.tterrag.com"
	}
	maven {
		name = "chickenbones"
		url = 'http://chickenbones.net/maven/'
	}
    maven {
        name "ProfMobius"
        url "http://mobiusstrip.eu/maven"
    }
}

dependencies {
	compile ic2Version
	compile mantleVersion
	compile ticVersion
	compile naturaVersion
	compile eioVersion
	compile codeChickenCoreVersion
	compile notEnoughItemsVersion
	compile codeChickenLibVersion

	compile fileTree(dir: 'fsplib', include: '*.jar') // Avoiding using the generic "lib".
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		filter {
			it.replaceAll('@VERSION@', project.version)
		}
	}

    from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
		exclude 'api'
		include 'steamcraft/api'
    }
}
